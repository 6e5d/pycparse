[
[S
	[+ Blocks]
]
[Blocks
	[!]
	[blocks Blocks Block]
]
[Block
	[+ Defun]
	[+ Typedef]
	[const "const" "static" Ptype "var" "=" Expr ";"]
]
[Typedef
	[typedef "typedef" Declare ";"]
	[typedef_su "typedef" "sue" "{" Declares "}" "type" ";"]
]
[Declares
	[declares. Declare ";"]
	[declares Declare ";" Declares]
]
[Defun
	[decfun Declare ";"]
	[defun Declare Procedure]
	[defun_static "static" Declare Procedure]
]
[Dparams
	[dparams. Declare]
	[dparams Dparams "," Declare]
]
[Declare
	[declare Ptype Dbody]
]
[StmtdecBodys
	[stmtdec_bodys. StmtdecBody]
	[stmtdec_bodys StmtdecBodys "," StmtdecBody]
]
[StmtdecBody
	[var Dbody]
	[set Dbody "=" Expr14]
	[sets Dbody "=" Sval]
]

# primitive type
[Ptype
	[sue "sue" "type"]
	[. "type"]
]

[Dbody
	[+ Dbody2]
	[ptr "*" Dbody]
]
[Dbody2
	[!]
	[+ Dbody3]
	[arg Dbody3 "(" Dparams ")"]
	[array Dbody2 "[" Lit "]"]
]
[Dbody3
	[. "var"]
	[paren "(" Dbody ")"]
]
[Procedure
	[begin "{" Stmts "}"]
]
[Stmts
	[!]
	[stmts Stmts Stmt]
]
[Stmt
	[+ Stmt2 ";"]
	[+ If]
	[+ Switch]
	[return "return" Expr ";"]
	[returnvoid "return" ";"]
	[continue "continue" ";"]
	[break "break" ";"]
	[while "while" "(" Expr ")" Procedure]
	[for "for" "(" Stmt2 ";" Stmt3 ";" Stmt3 ")" Procedure]
	[+ Procedure]
]
[Switch
	[switch "switch" "(" Expr ")" "{" Cases "}" ]
]
[Cases
	[cases. Case]
	[cases Cases Case]
]
[Case
	[case "case" Expr ":" Stmt]
	[default "default" ":" Stmt]
]
[If
	[if "if" "(" Expr ")" Procedure Ifcont]
]
[Ifcont
	[!]
	[elif "else" If]
	[else "else" Procedure]
]
# Stmt that can appear in for's first block
[Stmt2
	[+ Stmt3]
	[stmtdec Ptype StmtdecBodys]
]
# Stmt that can appear in for's second/third block
[Stmt3
	[!]
	[stmtexpr Expr]
]
[Expr
	[+ Expr15]
]
[Expr15
	[+ Expr14]
	[comma Expr15 "," Expr14]
]
[Expr14
	[+ Expr12]
	[assign Expr12 "opassign" Expr14]
	[assign Expr12 "=" Expr14]
]
[Expr12
	[+ Expr11]
	[binop Expr12 "||" Expr11]
]
[Expr11
	[+ Expr10]
	[binop Expr11 "&&" Expr10]
]
[Expr10
	[+ Expr9]
	[binop Expr10 "|" Expr9]
]
[Expr9
	[+ Expr8]
	[binop Expr9 "^" Expr8]
]
[Expr8
	[+ Expr7]
	[binop Expr8 "&" Expr7]
]
[Expr7
	[+ Expr6]
	[binop Expr7 "eqneq" Expr6]
]
[Expr6
	[+ Expr5]
	[binop Expr6 "relation" Expr5]
]
[Expr5
	[+ Expr4]
	[binop Expr5 "bitshift" Expr4]
]
[Expr4
	[+ Expr3]
	[binop Expr4 "add" Expr3]
]
[Expr3
	[+ Expr2]
	[binop Expr3 "divmod" Expr2]
	[binop Expr3 "*" Expr2]
]
[Expr2
	[+ Expr1]
	[&p "&" Expr2]
	[*p "*" Expr2]
	[prefix "prefix" Expr2]
	[cast "(" Declare ")" Expr2]
	[casts "(" Declare ")" Sval]
]
[Expr1
	[+ Lit]
	[paren "(" Expr ")"]
	[index Expr1 "[" Expr "]"]
	[callvoid Expr1 "(" ")"]
	[call Expr1 "(" Params ")"]
	[sizeof "sizeof" "(" Declare ")"]
	[field Expr1 "field" "var"]
]
[Params
	[params. Expr14]
	[params Params "," Expr14]
]
[Lit
	[+ Clit]
	[. "var"]
]
[Sval
	[brace_a "{" MemberA "}"]
	[brace_a "{" MemberA "," "}"]
	[brace_s "{" MemberS "}"]
	[brace_s "{" MemberS "," "}"]
]
[MemberA
	[member_a. Expr14]
	[member_a. Sval]
	[member_a MemberA "," Expr14]
	[member_a MemberA "," Sval]
]
[MemberS
	[member_s. Designated]
	[member_s MemberS "," Designated]
]
[Designated
	[designated "field" "var" "=" Expr14]
	[designated "field" "var" "=" Sval]
]
[Clit
	[numeric "num"]
	[strlit Strcat]
	[char "char"]
]
[Strcat
	[strcat. "str"]
	[strcat Strcat "str"]
]
]
